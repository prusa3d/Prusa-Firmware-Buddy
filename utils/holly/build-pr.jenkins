pipeline {
    agent {
        dockerfile {
            label 'docker'
            filename 'utils/holly/Dockerfile'
            additionalBuildArgs '-t prusa-firmware-buddy'
        }
    }

    parameters {
        string(name: 'VERSION_SUFFIX', defaultValue: '<default>', description: 'Specify custom version suffix for the build (e.g. "-RC1+1010"). Set to "<default>" to use the default one. Leave empty to make a final-version build without any suffix.')
        string(name: 'VERSION_SUFFIX_SHORT', defaultValue: '<default>', description: 'Specify custom version suffix for the build (e.g. "-RC1"). Set to "<default>" to use the default one. Leave empty to make a final-version build without any suffix.')
    }

    options {
        copyArtifactPermission('/Prusa-Firmware-Buddy/*')
    }

    stages {
        stage('Check Formatting') {
            when {
                expression { env.CHANGE_TARGET }
            }
            steps {
                catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                    sh """
                    ln -fs /work/.dependencies

                    export XDG_CACHE_HOME=\$PWD/.precommit
                    pre-commit install
                    pre-commit run \
                        --source remotes/origin/${env.CHANGE_TARGET} \
                        --origin HEAD \
                        --show-diff-on-failure \
                        --hook-stage manual
                    """
                }
            }
        }

        stage('Prepare Build Stages') {
            steps {
                script {
                    // required configurations
                    def configurations = [
                        [preset: "mini", build_type: "release", bootloader: "yes"],
                        [preset: "mk404-xBuddy027c-400step-planetary", build_type: "release", bootloader: "no"],
                        [preset: "xl", build_type: "release", bootloader: "yes"],
                        [preset: "xl-xbuddy04", build_type: "release", bootloader: "yes"],
                        [preset: "ix-xBuddy027c-200step", build_type: "release", bootloader: "empty"],
                    ]

                    // prepare version suffix
                    def commit_nr = sh(script: 'git rev-list HEAD --count', returnStdout: true).trim()
                    def short_suffix
                    def full_suffix
                    def dev_items
                    if (env.CHANGE_ID) {
                        // This is a PR build
                        short_suffix = "-BETA+${commit_nr}"
                        full_suffix = "${short_suffix}.PR${env.CHANGE_ID}.B${env.BUILD_NUMBER}"
                        dev_items = "yes"
                    } else if (env.BRANCH_NAME.startsWith("RELEASE-")) {
                        // This is an RC build
                        short_suffix = "-RC+${commit_nr}"
                        full_suffix = "${short_suffix}.B${env.BUILD_NUMBER}"
                        dev_items = "no"
                    } else {
                        // This is build of an ordinary branch (not a release branch)
                        short_suffix = "-BETA+${commit_nr}"
                        def branch_spec = env.BRANCH_NAME.replaceAll("_", "-")
                        full_suffix = "${short_suffix}.BRANCH-${branch_spec}.B${env.BUILD_NUMBER}"
                        dev_items = "yes"
                    }

                    if (env.BRANCH_NAME == "xl") {
                        dev_items = "no"
                    }

                    if (env.BRANCH_NAME == "mk404-release") {
                        dev_items = "no"
                    }

                    if (params.VERSION_SUFFIX != '<default>') {
                        full_suffix = params.VERSION_SUFFIX
                    }
                    if (params.VERSION_SUFFIX_SHORT != '<default>') {
                        short_suffix = params.VERSION_SUFFIX_SHORT
                    }

                    // create the build stages
                    configurations.each { config ->
                        stage("Build - ${config.preset},${config.build_type},${config.bootloader}boot") {
                            catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                                sshagent(credentials: ['prusa_holly']) {
                                    sh """
                                        [ -d ~/.ssh ] || mkdir ~/.ssh && chmod 0700 ~/.ssh
                                        ssh-keyscan -t rsa,dsa github.com >> ~/.ssh/known_hosts
                                    """
                                    sh """
                                        ln -fs /work/.dependencies
                                        python3 utils/build.py \
                                            --preset ${config.preset} \
                                            --build-type ${config.build_type} \
                                            --bootloader ${config.bootloader} \
                                            --generate-bbf \
                                            --generate-dfu \
                                            --no-store-output \
                                            --version-suffix=${full_suffix} \
                                            --version-suffix-short=${short_suffix} \
                                            -DCUSTOM_COMPILE_OPTIONS:STRING="-Werror" \
                                        -DDEVELOPMENT_ITEMS_ENABLED:BOOL=${dev_items}
                                    """
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Test') {
            steps {
                sh """
                export PATH=/work/.dependencies/cmake-3.22.5/bin:/work/.dependencies/ninja-1.10.2:\$PATH
                mkdir -p build-test
                LD_LIBRARY_PATH=/usr/local/lib32 /work/.dependencies/cmake-3.22.5/bin/ctest --build-and-test . build-test \
                    -DCMAKE_MAKE_PROGRAM=/work/.dependencies/ninja-1.10.2/ninja \
                    --build-generator Ninja \
                    --build-target tests \
                    --test-command ctest
                """
            }
        }
    }

    post {
        always {
            // archive build products
            archiveArtifacts artifacts: 'build/products/*', fingerprint: true
            // archive test products
            archiveArtifacts artifacts: 'build-test/Testing/Temporary/LastTest.log'
        }
        cleanup {
            deleteDir()
        }
    }
}
